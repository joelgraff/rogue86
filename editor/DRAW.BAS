1 LOCATE 1,1
2 PRINT "MOVEMENT.BAS..."
3 MERGE "MOVEMENT.BAS"
2000 '-------------------
2010 '---Screen Update---
2020 '-------------------
2021 IF FLAG.RECALC.ATTR = TRUE THEN GOSUB 2150
2022 IF FLAG.RESET.CRSR = TRUE THEN GOSUB 2420
2023 IF CHAR.COUNT > 0 THEN GOSUB 2050
2025 IF FLAG.REDRAW.CLEAR = TRUE THEN GOSUB 2650
2027 IF FLAG.REDRAW.ATTR = TRUE THEN GOSUB 2200
2029 IF FLAG.REDRAW.STAT = TRUE THEN GOSUB 2290
2030 IF FLAG.REFRESH.CRSR = TRUE THEN GOSUB 2600
2031 IF FLAG.REDRAW.CRSR = TRUE THEN GOSUB 2500
2049 RETURN
2050 '----------
2051 '--- DRAW NEW CHARACTERS
2057 DEF SEG = PAGE.CURRENT
2058 Y = 0
2060 FOR X = 0 TO CHAR.COUNT - 1
2070   POKE OFFSET.CURRENT + Y, CHAR.BUFFER(X)
2071   Z = ATTR.CURRENT
2072   IF CHAR.BUFFER(X) = 0 THEN Z = 0
2080   POKE OFFSET.CURRENT + Y + 1, Z
2090   Y = Y + 2
2100 NEXT
2110 OFFSET.DELTA = Y - 2
2120 CHAR.COUNT = 0
2130 FLAG.REFRESH.CRSR = OFFSET.DELTA = 0
2132 FLAG.REDRAW.CRSR = OFFSET.DELTA > 0
2140 RETURN
2150 '------------------------
2151 '--- RECALC ATTRIBUTE ---
2152 '------------------------
2160 FLAG.RECALC.ATTR = FALSE
2162 FLAG.RECALC.ATTR = FALSE
2164 FG = 0
2166 BG = 0
2168 I = 0
2170 B = 0
2172 FG = ATTR.FORE
2174 IF ATTR.INTENSE = TRUE THEN I = 8
2176 IF ATTR.BLINK = TRUE THEN B = 128
2178 BG = 16 * ATTR.BACK
2180 ATTR.CURRENT = FG OR I OR BG OR B
2182 RETURN
2200 '-----------------------
2201 '--- REDRAW ATTRIBUTE --
2202 '-----------------------
2210 DEF SEG = PAGE.CURRENT
2220 POKE OFFSET.CURRENT + 1, ATTR.CURRENT
2230 FLAG.REDRAW.ATTR = FALSE
2240 FLAG.REFRESH.CRSR = TRUE
2250 RETURN
2290 '----------------------
2300 '---REDRAW STATUS BAR---
2310 '----------------------
2320 FLAG.REDRAW.STAT = FALSE
2330 DEF SEG = PAGE.CURRENT - &HA
2340 FOR X = 0 TO 158 STEP 2
2350   POKE X, 0
2360 POKE X + 1, 112
2370 NEXT
2380 POKE 156, PAGE.INDEX + 49
2381 COLOR 0,7
2382 SPACES$ = ""
2383 IF PAGE.ROW < 10 THEN SPACES$ = SPACES$ + " "
2384 IF PAGE.COL < 10 THEN SPACES$ = SPACES$ + " "
2385 LOCATE 1,65,0:PRINT STAT.POSITION$
2386 LOCATE 1,1:PRINT"RogueEdit v" + VERSION$
2389 COLOR 7,0
2390 POKE 152, KEYPAD.MAP(KEYPAD.MODE, 76)
2395 POKE 153, ATTR.CURRENT
2400 RETURN
2410 '-------------------
2420 '---RESET CURSOR---
2430 '-------------------
2440 FLAG.RESET.CRSR = FALSE
2450 OFFSET.CURRENT = 1998
2470 GOSUB 2500
2480 RETURN
2490 '--------------------
2500 '--- REDRAW CURSOR ---
2510 '---------------------
2520 FLAG.REDRAW.CRSR = FALSE
2530 IF OFFSET.DELTA = 0 THEN RETURN
2540 DEF SEG = PAGE.CURRENT
2550 POKE OFFSET.CURRENT + 1, PEEK(OFFSET.CURRENT + 1) XOR ATTR.INVERT
2560 OFFSET.CURRENT = OFFSET.CURRENT + OFFSET.DELTA
2570 POKE OFFSET.CURRENT + 1, PEEK(OFFSET.CURRENT + 1) XOR ATTR.INVERT
2580 OFFSET.DELTA = 0
2582 COLOR 0,7
2583 PAGE.ROW = INT(OFFSET.CURRENT / 160) + 1
2584 PAGE.COL = INT((OFFSET.CURRENT MOD 160) / 2) + 1
2585 STAT.POSITION$ = STR$(PAGE.ROW)+","+STR$(PAGE.COL)
2586 IF PAGE.ROW < 10 THEN STAT.POSITION$ = STAT.POSITION$ + " "
2587 IF PAGE.COL < 10 THEN STAT.POSITION$ = STAT.POSITION$ + " "
2588 LOCATE 1,65,0:PRINT STAT.POSITION$
2590 COLOR 7,0
2599 RETURN
2600 '--------------------------
2601 '--- REFRESH THE CURSOR ---
2602 '--------------------------
2605 DEF SEG = PAGE.CURRENT
2610 FLAG.REFRESH.CRSR = FALSE
2620 POKE OFFSET.CURRENT + 1, PEEK(OFFSET.CURRENT + 1) XOR ATTR.INVERT
2630 RETURN
2650 '------------------------
2651 '--- CLEAR THE SCREEN ---
2652 '------------------------
2653 FLAG.REDRAW.CLEAR = FLASE
2654 COLOR 7,0: CLS
2655 FLAG.REDRAW.STAT = TRUE
2656 FLAG.REFRESH.CRSR = TRUE
2660 RETURN
