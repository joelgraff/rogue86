1 LOCATE 1,1:PRINT"ATTRIB.BAS...          "
2 MERGE "ATTRIB.BAS"
5000 '--- DELEGATE KEY PRESS ---
5010 IF CMD.ACTIVE = CMD.KPD THEN GOTO 5300
5020 IF CMD.ACTIVE = CMD.KBD.DEL THEN GOTO 5100
5025 IF KEY.PRESS >=252 AND KEY.PRESS <=260 THEN GOTO 5400
5030 IF KEY.PRESS >= 271 AND KEY.PRESS <= 283 THEN GOTO 5200
5040 IF CMD.ACTIVE = CMD.KBD.LOAD THEN GOTO 4200
5050 IF CMD.ACTIVE = CMD.KBD.SAVE THEN GOTO 4000
5055 IF CMD.ACTIVE = CMD.KBD.CLEAR THEN FLAG.REDRAW.CLEAR = TRUE
5060 IF CMD.ACTIVE = CMD.KBD.QUIT THEN QUIT.LOOP = TRUE
5090 RETURN
5100 '--- DELETE CURRENT CHARACTER ---
5110 CHAR.BUFFER(CHAR.COUNT) = 0
5120 CHAR.COUNT = CHAR.COUNT + 1
5130 FLAG.REFRESH.CRSR = TRUE
5140 RETURN
5200 '--- PRINT KEYPAD SYMBOL ---
5210 C = KEYPAD.MAP(KEYPAD.MODE,KEY.PRESS - 200)
5220 CHAR.BUFFER(CHAR.COUNT) = C
5230 CHAR.COUNT = CHAR.COUNT + 1
5240 FLAG.REFRESH.CRSR = TRUE
5250 RETURN
5300 '--- ALT-ENTER MODE SWITCH ---
5310 KEYPAD.MODE = KEYPAD.MODE + 1
5320 IF KEYPAD.MODE > 4 THEN KEYPAD.MODE = 0
5330 FLAG.REDRAW.STAT = TRUE
5340 RETURN
5400 '--- PRINT KEYBOARD CHARACTERS ---
5410 C = KEYBOARD.MAP(KEY.PRESS)
5420 IF C = 0 THEN RETURN
5430 CHAR.BUFFER(CHAR.COUNT) = C
5440 CHAR.COUNT = CHAR.COUNT + 1
5450 RETURN
