1 LOCATE 1,1:PRINT"UTILS.BAS...          "
2 MERGE "UTILS.BAS"
6000 '--- KEY PRESS DELEGATION ---
6010 IF CMD.ACTIVE = CMD.ATR.FC.UP OR CMD.ACTIVE = CMD.ATR.FC.DN THEN GOTO 6100
6020 IF CMD.ACTIVE = CMD.ATR.BC.UP OR CMD.ACTIVE = CMD.ATR.BC.DN THEN GOTO 6200
6030 IF CMD.ACTIVE = CMD.ATR.BL THEN GOTO 6300
6040 IF CMD.ACTIVE = CMD.ATR.HI THEN GOTO 6350
6050 IF CMD.ACTIVE = CMD.ATR.DUP THEN GOTO 6400
6080 FLAG.REDRAW.ATTR = (CMD.ACTIVE = CMD.ATR.SET)
6090 RETURN
6100 '--- FOREGROUND COLOR ---
6110 DELTA = 1
6120 IF CMD.ACTIVE = CMD.ATR.FC.DN THEN DELTA = -1
6130 ATTR.FORE = ATTR.FORE + DELTA
6140 IF ATTR.FORE > 7 THEN ATTR.FORE = 7
6150 IF ATTR.FORE < 0 THEN ATTR.FORE = 0
6160 FLAG.RECALC.ATTR = TRUE
6170 FLAG.REDRAW.STAT = TRUE
6180 RETURN
6190 PRINT CMD.ACTIVE, ATTR.FORE
6200 '--- BACKGROUND COLOR ---
6210 DELTA = 1
6220 IF CMD.ACTIVE = CMD.ATR.BC.DN THEN DELTA = -1
6230 ATTR.BACK = ATTR.BACK + DELTA
6240 IF ATTR.BACK > 7 THEN ATTR.BACK = 7
6250 IF ATTR.BACK < 0 THEN ATTR.BACK = 0
6260 FLAG.RECALC.ATTR = TRUE
6270 FLAG.REDRAW.STAT = TRUE
6280 RETURN
6300 '--- TOGGLE BLINKING ---
6310 IF ATTR.BLINK = TRUE THEN ATTR.BLINK = FALSE ELSE ATTR.BLINK = TRUE
6320 FLAG.RECALC.ATTR = TRUE
6330 FLAG.REDRAW.STAT = TRUE
6340 RETURN
6350 '--- TOGGLE INTENSITY ---
6360 IF ATTR.INTENSE = TRUE THEN ATTR.INTENSE = FALSE ELSE ATTR.INTENSE = TRUE
6370 FLAG.RECALC.ATTR = TRUE
6380 FLAG.REDRAW.STAT = TRUE
6390 RETURN
6400 '--- DUPLICATE ATTRIBUTE ---
6410 ATTR.CURRENT = PEEK (OFFSET.CURRENT + 1) XOR ATTR.INVERT
6420 FLAG.REDRAW.STAT = TRUE
6430 RETURN
